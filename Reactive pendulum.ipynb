{
 "cells": [
  {
   "cell_type": "code",
   "execution_count": 1,
   "id": "c4836ada",
   "metadata": {},
   "outputs": [],
   "source": [
    "using GLMakie"
   ]
  },
  {
   "cell_type": "code",
   "execution_count": 2,
   "id": "17cc1929",
   "metadata": {},
   "outputs": [],
   "source": [
    "using RxInfer, Rocket\n",
    "\n",
    "import ReactiveMP: getrecent, messageout\n",
    "import Rocket: subscribe!\n",
    "import Base: show"
   ]
  },
  {
   "cell_type": "code",
   "execution_count": 50,
   "id": "582c9a52",
   "metadata": {},
   "outputs": [],
   "source": [
    "# Dynamical parameters\n",
    "m = 0.65 # grams\n",
    "l = 0.85 # cm\n",
    "b = 0.6 # friction\n",
    "g = 9.81 # gravity\n",
    "N = 200\n",
    "# Time horizon\n",
    "T = 20\n",
    "c = 6.0\n",
    "S = 1e-2 # World noise variance\n",
    "\n",
    "#C = 4, T = 30\n",
    "#C = 5, T = 5\n",
    "\n",
    "# Time step size\n",
    "# Δt = 0.007;\n",
    "Δt = 1 / 60;"
   ]
  },
  {
   "cell_type": "code",
   "execution_count": 51,
   "id": "7e47e343",
   "metadata": {},
   "outputs": [
    {
     "data": {
      "text/plain": [
       "f_arctanh (generic function with 1 method)"
      ]
     },
     "execution_count": 51,
     "metadata": {},
     "output_type": "execute_result"
    }
   ],
   "source": [
    "# Internal dynamic model\n",
    "# We asume the dynamical model is known for now\n",
    "# Its interesting to extend this further and try to actually infer all parameters\n",
    "function dzdt(z_t_min, u) \n",
    "    # Transition function modeling transition due to gravity, friction and engine control\n",
    "    (θ, θ̇) = z_t_min\n",
    "    θ̈ = 1/(m*l^2)*(-m*g*l*sin(θ) - b*θ̇ .+ u)\n",
    "    Δz = [ θ̇, θ̈ ]\n",
    "    z_t = z_t_min .+  Δz .* Δt\n",
    "    return z_t\n",
    "end\n",
    "\n",
    "f_tanh(u)    = c * tanh(u)\n",
    "f_arctanh(ū) = atanh(clamp(ū, -c+1e-3, c-1e-3) / c)"
   ]
  },
  {
   "cell_type": "code",
   "execution_count": 52,
   "id": "0e2fda4c",
   "metadata": {},
   "outputs": [
    {
     "data": {
      "text/plain": [
       "tick (generic function with 1 method)"
      ]
     },
     "execution_count": 52,
     "metadata": {},
     "output_type": "execute_result"
    }
   ],
   "source": [
    "# BEHOLD THE WORLD\n",
    "mutable struct PendulumWorld\n",
    "    real_pendulum_position :: Float64\n",
    "    real_pendulum_velocity :: Float64\n",
    "    next_registered_action :: Float64\n",
    "    real_observations      :: Any\n",
    "    noisy_observations     :: Any\n",
    "    \n",
    "    PendulumWorld() = new(0.0, 0.0, 0.0, RecentSubject(Float64), RecentSubject(Float64))\n",
    "end\n",
    "\n",
    "Base.show(io::IO, world::PendulumWorld) = print(io, \"PendulumWorld()\")\n",
    "    \n",
    "function register_next_action(world::PendulumWorld, action)\n",
    "    world.next_registered_action = action\n",
    "end\n",
    "\n",
    "# `tick` function is used to move the state of the world further and is independed from any agent\n",
    "# An agent can only `register` a new action in between with the `register_next_action`\n",
    "function tick(world::PendulumWorld)\n",
    "    hidden_state = (world.real_pendulum_position, world.real_pendulum_velocity)\n",
    "    next_hidden_state = dzdt(hidden_state, f_tanh(world.next_registered_action))\n",
    "            \n",
    "    world.next_registered_action = 0.0\n",
    "    world.real_pendulum_position = next_hidden_state[1]\n",
    "    world.real_pendulum_velocity = next_hidden_state[2]\n",
    "        \n",
    "    noisy_observation = mod(rand(NormalMeanVariance(world.real_pendulum_position, S)), 2pi)\n",
    "        \n",
    "    next!(world.real_observations, world.real_pendulum_position)\n",
    "    next!(world.noisy_observations, noisy_observation)\n",
    "end"
   ]
  },
  {
   "cell_type": "code",
   "execution_count": 53,
   "id": "3af4a730",
   "metadata": {},
   "outputs": [
    {
     "data": {
      "text/plain": [
       "pendulum_meta (generic function with 1 method)"
      ]
     },
     "execution_count": 53,
     "metadata": {},
     "output_type": "execute_result"
    }
   ],
   "source": [
    "@model function pendulum(T)\n",
    "    # Internal model parameters\n",
    "    Gamma = constvar(1e10 * diageye(2)) # Transition precision\n",
    "    cO = constvar([1.0; 0.0]) # Observation matrix\n",
    "    \n",
    "    # Previous state prior\n",
    "    m_s_t_min = datavar(Vector{Float64})\n",
    "    V_s_t_min = datavar(Matrix{Float64})\n",
    "    \n",
    "    # Previous action prior\n",
    "    m_u_t_min = datavar(Float64)\n",
    "    v_u_t_min = datavar(Float64)\n",
    "    \n",
    "    # Current observation\n",
    "    x_t = datavar(Float64)\n",
    "    \n",
    "    # Future control priors\n",
    "    m_u = datavar(Float64, T)\n",
    "    V_u = datavar(Float64, T)\n",
    "    \n",
    "    # Future goal priors\n",
    "    m_x = datavar(Float64, T)\n",
    "    V_x = datavar(Float64, T)\n",
    "    \n",
    "    u   = randomvar(T) # Future actions\n",
    "    u_s = randomvar(T) # Future deterministic states\n",
    "    s   = randomvar(T) # Future states with uncertainty\n",
    "    x   = randomvar(T) # Future observations\n",
    "\n",
    "    s_t_min ~ MvNormal(mean = m_s_t_min, cov = V_s_t_min) # Prior for previous state\n",
    "    u_t_min ~ Normal(mean = m_u_t_min, var = v_u_t_min)   # Prior for previous action\n",
    "    u_s_min ~ dzdt(s_t_min, f_tanh(u_t_min))              # Deterministic state transition function\n",
    "    s_t     ~ MvNormal(mean = u_s_min, precision = Gamma) # Transition uncertainty\n",
    "    x_t     ~ Normal(mean = dot(cO, s_t), variance = S)   # Observational function\n",
    "    \n",
    "    s_k_min = s_t\n",
    "    \n",
    "    for k in 1:T\n",
    "        u[k]    ~ Normal(mean = m_u[k], var = V_u[k])         #\n",
    "        u_s[k]  ~ dzdt(s_k_min, f_tanh(u[k]))\n",
    "        s[k]    ~ MvNormal(mean = u_s[k], precision = Gamma)\n",
    "        x[k]    ~ Normal(mean = dot(cO, s[k]), variance = S)\n",
    "        x[k]    ~ Normal(mean = m_x[k], variance = V_x[k]) \n",
    "        s_k_min = s[k]\n",
    "    end\n",
    "    \n",
    "    return (s, )\n",
    "end\n",
    "\n",
    "@meta function pendulum_meta()\n",
    "    dzdt() -> DeltaMeta(method = Linearization())\n",
    "    f_tanh() -> DeltaMeta(method = Unscented(kappa=1e-2), inverse=f_arctanh)\n",
    "end\n"
   ]
  },
  {
   "cell_type": "code",
   "execution_count": 62,
   "id": "6c6652ba",
   "metadata": {},
   "outputs": [],
   "source": [
    "mutable struct SuperSmartRxInferAgent\n",
    "    rxinfer_engine           :: Union{Nothing, RxInferenceEngine}\n",
    "    the_goal_in_radians      :: Float64 \n",
    "    the_goal_variance        :: Float64\n",
    "    mean_control_priors      :: Vector{Float64}\n",
    "    var_control_priors       :: Vector{Float64}\n",
    "    mean_goal_priors         :: Vector{Float64}\n",
    "    var_goal_priors          :: Vector{Float64}\n",
    "    mean_current_state_prior :: Vector{Float64}\n",
    "    cov_current_state_prior  :: Matrix{Float64}\n",
    "    \n",
    "    function SuperSmartRxInferAgent(T)\n",
    "        mean_control_priors = Float64[ 0.0 for _ in 1:T ]\n",
    "        var_control_priors  = Float64[ huge for _ in 1:T ]\n",
    "\n",
    "        mean_goal_priors = Float64[ 0.0 for _ in 1:T ]\n",
    "        var_goal_priors = Float64[ huge for _ in 1:T ]\n",
    "\n",
    "        the_goal_in_radians = 3.14\n",
    "        the_goal_variance   = 1e-4\n",
    "\n",
    "        mean_current_state_prior = [ 0.0, 0.0 ]\n",
    "        cov_current_state_prior  = tiny * diageye(2)\n",
    "\n",
    "        return new(\n",
    "            nothing, \n",
    "            the_goal_in_radians, \n",
    "            the_goal_variance,\n",
    "            mean_control_priors,\n",
    "            var_control_priors,\n",
    "            mean_goal_priors,\n",
    "            var_goal_priors,\n",
    "            mean_current_state_prior,\n",
    "            cov_current_state_prior\n",
    "        )\n",
    "    end\n",
    "end\n",
    "\n",
    "Base.show(io::IO, agent::SuperSmartRxInferAgent) = print(io, \"SuperSmartRxInferAgent()\")\n",
    "\n",
    "# Shift a vector and put a new value at the end\n",
    "function shift(vector, value)\n",
    "    return (_) -> begin \n",
    "        @inbounds for i in firstindex(vector):lastindex(vector)-1\n",
    "            vector[i] = vector[i + 1]\n",
    "        end\n",
    "        vector[end] = value\n",
    "        return vector\n",
    "    end\n",
    "end\n",
    "\n",
    "function Rocket.subscribe!(agent::SuperSmartRxInferAgent, datastream)\n",
    "    T = length(agent.mean_control_priors)\n",
    "    \n",
    "    recent_action = RecentSubject(Float64)\n",
    "        \n",
    "    next!(recent_action, 0.0)\n",
    "    \n",
    "    shift_mean_control_priors = shift(agent.mean_control_priors, 0.0)\n",
    "    shift_var_control_priors  = shift(agent.var_control_priors, huge)\n",
    "    shift_mean_goal_priors    = shift(agent.mean_goal_priors, agent.the_goal_in_radians)\n",
    "    shift_var_goal_priors     = shift(agent.var_goal_priors, agent.the_goal_variance)\n",
    "    pick_first_action         = (actions) -> mean_var(first(actions))\n",
    "    \n",
    "    # A simple logic to update the agent's prior automatically\n",
    "    autoupdates = @autoupdates begin \n",
    "        m_u = shift_mean_control_priors(q(u))\n",
    "        V_u = shift_var_control_priors(q(u))\n",
    "        m_x = shift_mean_goal_priors(q(x))\n",
    "        V_x = shift_var_goal_priors(q(x))\n",
    "        m_u_t_min, v_u_t_min = pick_first_action(q(u))\n",
    "    end\n",
    "        \n",
    "    initial_forces = map(agent.mean_current_state_prior, agent.var_control_priors) do m, v\n",
    "         return NormalMeanVariance(m, v)\n",
    "    end\n",
    "        \n",
    "    engine = rxinference(\n",
    "        model = pendulum(T),\n",
    "        meta = pendulum_meta(),\n",
    "        datastream = datastream,\n",
    "        autoupdates = autoupdates,\n",
    "        initmarginals = (u = initial_forces,),\n",
    "        autostart = false,\n",
    "        returnvars = (:u, ),\n",
    "    )\n",
    "        \n",
    "    update!(engine.model[:m_s_t_min], agent.mean_current_state_prior)\n",
    "    update!(engine.model[:V_s_t_min], agent.cov_current_state_prior)\n",
    "    \n",
    "    # Slide logic, a bit tricky, we need to create a proper API for this\n",
    "    slide_callback = (_) -> begin\n",
    "        slide_msg_idx = 3        # This is model dependent\n",
    "        (s, ) = engine.returnval # Retrieve a reference to the `states` posteriors\n",
    "        predictive_message = getrecent(messageout(s[1], slide_msg_idx)) # Get a predictive message\n",
    "            \n",
    "        (m_s_t_min, V_s_t_min) = mean_cov(predictive_message) \n",
    "    \n",
    "        agent.mean_current_state_prior = m_s_t_min\n",
    "        agent.cov_current_state_prior = V_s_t_min\n",
    "            \n",
    "        update!(engine.model[:m_s_t_min], agent.mean_current_state_prior)\n",
    "        update!(engine.model[:V_s_t_min], agent.cov_current_state_prior)\n",
    "    end\n",
    "    \n",
    "    slide_subscription = subscribe!(engine.posteriors[:u], slide_callback)\n",
    "        \n",
    "    recent_action_subscription = subscribe!(engine.posteriors[:u], (actions) -> begin \n",
    "        next!(recent_action, mode(first(actions)))\n",
    "    end)\n",
    "    \n",
    "    agent.rxinfer_engine = engine\n",
    "    \n",
    "    RxInfer.start(engine)\n",
    "    \n",
    "    return recent_action, () -> begin \n",
    "        unsubscribe!(slide_subscription)\n",
    "        unsubscribe!(recent_action_subscription)\n",
    "        RxInfer.stop(agent.rxinfer_engine)\n",
    "        agent.rxinfer_engine = nothing\n",
    "    end\n",
    "end"
   ]
  },
  {
   "cell_type": "code",
   "execution_count": 63,
   "id": "fd324a4e",
   "metadata": {},
   "outputs": [
    {
     "data": {
      "text/plain": [
       "animstep! (generic function with 1 method)"
      ]
     },
     "execution_count": 63,
     "metadata": {},
     "output_type": "execute_result"
    }
   ],
   "source": [
    "function animstep!(rod,Θ)\n",
    "    \n",
    "    rod[] = [Point2f(0, 0), Point2f(l*sin(Θ), -l*cos(Θ))]\n",
    "    balls[] = [Point2f(l*sin(Θ), -l*cos(Θ))]\n",
    "\n",
    "end"
   ]
  },
  {
   "cell_type": "code",
   "execution_count": 64,
   "id": "e13e6928",
   "metadata": {},
   "outputs": [
    {
     "data": {
      "text/plain": [
       "ObserverFunction defined at In[64]:51 operating on Observable{Any}(0)"
      ]
     },
     "execution_count": 64,
     "metadata": {},
     "output_type": "execute_result"
    }
   ],
   "source": [
    "x1 = 0.5\n",
    "y1 = 0.5\n",
    "rod   = Observable([Point2f(0, 0), Point2f(x1, y1)])\n",
    "balls = Observable([Point2f(x1, y1)])\n",
    "fig = Figure(); display(fig)\n",
    "ax = Axis(fig[1,1])\n",
    "\n",
    "lines!(ax, rod; linewidth = 4, color = :blue)\n",
    "scatter!(ax, balls; marker = 'o', strokewidth = 2,\n",
    "    strokecolor = :black,\n",
    "    color = :black, markersize = [30]\n",
    ")\n",
    "xlims!(ax, -1.5l, 1.5l)\n",
    "ylims!(ax, -1.5l, 1.5l)\n",
    "ax.title = \"Pendulum\"\n",
    "ax.aspect = DataAspect()\n",
    "\n",
    "world = PendulumWorld()\n",
    "\n",
    "agent = SuperSmartRxInferAgent(T)\n",
    "\n",
    "datastream = labeled(Val((:x_t, )), combineLatest(world.noisy_observations))\n",
    "\n",
    "action, subscription = subscribe!(agent, datastream);\n",
    "\n",
    "action_subscription = subscribe!(action, (a) -> register_next_action(world, a))\n",
    "\n",
    "tick(world)\n",
    "\n",
    "animstep!(rod, Rocket.getrecent(world.real_observations))\n",
    "# sleep(0.01)\n",
    "\n",
    "# The run button is actually pretty simple, we'll add it below the plot\n",
    "run = Button(fig[2,1]; label = \"run\", tellwidth = false)\n",
    "stop = Button(fig[3,1]; label = \"stop\", tellwidth = false)\n",
    "# This button will start/stop an animation. It's actually surprisingly\n",
    "# simple to do this. The magic code is:\n",
    "isrunning = Observable(true)\n",
    "\n",
    "on(run.clicks) do clicks\n",
    "    isrunning[] = true\n",
    "end\n",
    "\n",
    "on(stop.clicks) do clicks\n",
    "    isrunning[] = false\n",
    "    unsubscribe!(subscription)\n",
    "    unsubscribe!(action_subscription)\n",
    "end\n",
    "\n",
    "on(run.clicks) do clicks\n",
    "    @async begin \n",
    "        try \n",
    "            while isrunning[]\n",
    "                global y2\n",
    "                global a2\n",
    "\n",
    "                isopen(fig.scene) || break # ensures computations stop if closed window\n",
    "\n",
    "                # a2 = Rocket.getrecent(action) # Evoke an action from the agent\n",
    "                # y2 = act(world, a2) # The action influences hidden external states\n",
    "                # next!(ys, y2) \n",
    "                tick(world)\n",
    "                animstep!(rod, Rocket.getrecent(world.real_observations))\n",
    "                sleep(Δt)\n",
    "            end\n",
    "        catch err\n",
    "            bt = catch_backtrace()\n",
    "            println()\n",
    "            showerror(stderr, err, bt)\n",
    "        end\n",
    "        unsubscribe!(subscription)\n",
    "        unsubscribe!(action_subscription)\n",
    "    end\n",
    "end\n",
    "\n",
    "# on(ax.scene.events.mousebutton) do mpos\n",
    "#     #global a2\n",
    "#     global y2\n",
    "    \n",
    "#     if ispressed(ax.scene, Mouse.left)\n",
    "#        pos = to_world(ax.scene, Point2f(ax.scene.events.mouseposition[]))\n",
    "#        #print(pos)\n",
    "#        y2 = atan(pos[2],pos[1]) + π/2 \n",
    "#        balls[] = [Point2f(pos[1]-1, pos[2]-0.5)]\n",
    "#        rod[] = [Point2f(0, 0), Point2f(pos[1]-1, pos[2]-0.5)]\n",
    "        \n",
    "#    end\n",
    "#    return\n",
    "# end"
   ]
  },
  {
   "cell_type": "code",
   "execution_count": null,
   "id": "9fcfd5cb",
   "metadata": {},
   "outputs": [],
   "source": []
  },
  {
   "cell_type": "markdown",
   "id": "2034130b",
   "metadata": {},
   "source": [
    "**Credits** The original code is written by Sepideh Adamiat. Adapted by Dmitry Bagaev"
   ]
  }
 ],
 "metadata": {
  "kernelspec": {
   "display_name": "Julia 1.8.4",
   "language": "julia",
   "name": "julia-1.8"
  },
  "language_info": {
   "file_extension": ".jl",
   "mimetype": "application/julia",
   "name": "julia",
   "version": "1.8.4"
  }
 },
 "nbformat": 4,
 "nbformat_minor": 5
}
